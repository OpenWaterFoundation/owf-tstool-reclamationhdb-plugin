# Test writing a month time series to ReclamationHDB database:
#   - development database
#   - new WRITE_MODEL_DATA procedure
#   - model table
#   - SDI 100376 (site common name AAA_DELETE)
#   - model name (MRI not specified) - corresponds to MRI 167
#   - month data - note that HDB uses start_date_time for month (end is day one of next month)
#   - Write twice with different numbers to make sure old results are not just sitting
#     in the database.  Write non-missing values because HDB convention is to
#     NOT write missing values (not sure how re-write is handled if missing involved).
#   - For two writes, use TSID read once, and ReadReclamationHDB() once to exercise read.
#@testSuite ReclamationHDB
StartLog(LogFile="Results/Test_WriteReclamationHDB_WriteData_Dev_Month_AAADELETE_Model_Name.TSTool.log")
RemoveFile(InputFile="Results/Test_ReclamationHDB_WriteData_Dev_Month_AAADELETE_Model_Name_out.dv",IfNotFound=Ignore)
#
#------------------------- start the first pass -------------------------------------
SetInputPeriod(InputStart="2010-01",InputEnd="2015-02")
SetOutputPeriod(OutputStart="2010-01",OutputEnd="2015-02")
SetProperty(PropertyName="InputEnd2",PropertyType=DateTime,PropertyValue="2015-03")
NewPatternTimeSeries(Alias="AAA_DELETE",NewTSID="AAA_DELETE.HDB.flow.Month",Description="AAA_DELETE",Units="cfs",PatternValues="600,1100,1300,1400,7600")
# First write the time series to the database...
WriteReclamationHDB(DataStore="hdbtest",SiteCommonName="AAA_DELETE",DataTypeCommonName="flow",SiteDataTypeID=100376,ModelRunID=167,Agency="USBR",ValidationFlag="A",TimeZone="MST",WriteProcedure="WRITE_DATA")
# Now read the same time series from the database using the period that was written...
ReadTimeSeries(TSID="diversion:100376-167.HDB.flow.Month.AAA_DELETE~hdbtest",Alias="FirstRead",IfNotFound=Warn)
# Compare the time series read as a time series with a time series created from SQL query output.
ReadTableFromDataStore(DataStore="hdbtest",Sql="select * from M_MONTH where site_datatype_id = 100376 and model_run_id = 167 and end_date_time >= to_date('${InputStart}','YYYY-MM') and end_date_time <= to_date('${InputEnd2}','YYYY-MM') order by start_date_time",TableID="r_month1")
TableToTimeSeries(TableID="r_month1",DateTimeColumn="start_date_time",LocationID="100376",ValueColumn="value",Interval=Month,Units="cfs",Alias="FirstReadFromTable")
CompareTimeSeries(TSID1="AAA_DELETE",TSID2="FirstRead",WarnIfDifferent=True)
CompareTimeSeries(TSID1="FirstRead",TSID2="FirstReadFromTable",WarnIfDifferent=True)
# Also check for missing because there should be none over time zone change
CheckTimeSeries(TSList=AllMatchingTSID,TSID="FirstRead",CheckCriteria="Missing")
CheckTimeSeries(TSList=AllMatchingTSID,TSID="FirstReadFromTable",CheckCriteria="Missing")
#
#------------------------- start the second pass -------------------------------------
# Start second pass (previous values x 10)...
NewPatternTimeSeries(Alias="AAA_DELETE",NewTSID="AAA_DELETE.HDB.flow.Month",Description="AAA_DELETE",Units="cfs",PatternValues="60000,20000,22000,23000,85000")
# First write the time series to the database...
WriteReclamationHDB(DataStore="hdbtest",TSList=LastMatchingTSID,TSID="AAA_DELETE",SiteCommonName="AAA_DELETE",DataTypeCommonName="flow",SiteDataTypeID=100376,ModelRunID=167,Agency="USBR",ValidationFlag="A",TimeZone="MST",WriteProcedure="WRITE_DATA")
# Now read the same time series from the database using the period that was written...
ReadReclamationHDB(DataStore="hdbtest",Interval="Month",SiteCommonName="AAA_DELETE",DataTypeCommonName="flow",SiteDataTypeID=100376,ModelRunID=167,Alias="SecondRead")
# Compare the time series read as a time series with a time series created from SQL query output.
ReadTableFromDataStore(DataStore="hdbtest",Sql="select * from M_MONTH where site_datatype_id = 100376 and model_run_id = 167 and start_date_time >= to_date('${InputStart}','YYYY-MM') and start_date_time <= to_date('${InputEnd2}','YYYY-MM') order by start_date_time",TableID="r_month2")
TableToTimeSeries(TableID="r_month2",DateTimeColumn="start_date_time",LocationID="100376",ValueColumn="value",Interval=Month,Units="cfs",Alias="SecondReadFromTable")
CompareTimeSeries(TSID1="AAA_DELETE",TSID2="SecondRead",WarnIfDifferent=True)
CompareTimeSeries(TSID1="SecondRead",TSID2="SecondReadFromTable",WarnIfDifferent=True)
# Also check for missing because there should be none over time zone change
CheckTimeSeries(TSList=AllMatchingTSID,TSID="SecondRead",CheckCriteria="Missing")
CheckTimeSeries(TSList=AllMatchingTSID,TSID="SecondReadFromTable",CheckCriteria="Missing")
#------------------------- end the second pass -------------------------------------
#
# Uncomment the following command to regenerate the expected results file.
# WriteDateValue(TSList=LastMatchingTSID,TSID="SecondRead",OutputFile="ExpectedResults/Test_WriteReclamationHDB_WriteData_Dev_Month_AAADELETE_Model_Name_out.dv")
# Generate new output for what was read from the database...
WriteDateValue(TSList=LastMatchingTSID,TSID="SecondRead",OutputFile="Results/Test_WriteReclamationHDB_WriteData_Dev_Month_AAADELETE_Model_Name_out.dv")
# Do regression comparison...
CompareFiles(InputFile1="ExpectedResults\Test_WriteReclamationHDB_WriteData_Dev_Month_AAADELETE_Model_Name_out.dv",InputFile2="Results/Test_WriteReclamationHDB_WriteData_Dev_Month_AAADELETE_Model_Name_out.dv",IfDifferent=Warn)
